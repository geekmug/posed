/*
 * Copyright (C) 2019, Scott Dial, All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'pl.allegro.tech.build.axion-release'

scmVersion {
  tag {
    prefix = 'v'
    versionSeparator = ''
  }
}

allprojects {
  project.version = scmVersion.version
}

release.dependsOn dist

allprojects {
  apply plugin: 'maven-publish'

  publishing {
    repositories {
      maven {
        credentials {
          username = "${artifactory_user}"
          password = "${artifactory_password}"
        }
        url "${artifactory_contextUrl}/" + (version.contains("SNAPSHOT") ? 'libs-snapshot-local' : 'libs-release-local')
      }
    }
  }

  if (jar.enabled) {
    publishing {
      publications {
        maven(MavenPublication) {
          from components.java
          if (project.hasProperty('sourcesJar')) {
            artifact sourcesJar
          }
          if (project.hasProperty('javadocJar')) {
            artifact javadocJar
          }

          pom {
            licenses {
              license {
                name = 'Apache License, Version 2.0'
                url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution = 'repo'
              }
            }
          }
        }
      }
    }
  }
}
